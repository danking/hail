buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
}

plugins {
  id 'scala'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

repositories {
  mavenCentral()
  jcenter()
  maven {
    url "https://oss.sonatype.org/content/repositories/iovertx-3868/"
  }
}

compileScala {
   options.compilerArgs <<
      "-Xlint:all" <<
      "-Werror" <<
      "-XDenableSunApiLintControl" <<
      "-Xlint:-sunapi" <<
      "-Xlint:-path" // Apparently we try to find some libraries that aren't always installed

   scalaCompileOptions.additionalParameters = [
      "-target:jvm-1.8",
      "-feature",
      "-Xno-patmat-analysis",
      "-Xfatal-warnings",
      "-Xlint:_",
      "-deprecation",
      "-unchecked",
      "-Xlint:-infer-any",
      "-Xlint:-unsound-match"
   ]

   scalaCompileOptions.forkOptions.with {
      jvmArgs = ["-Xms512M",
                 "-Xmx4096M",
                 "-Xss4M",
                 "-XX:MaxMetaspaceSize=1024M"]
   }
}

dependencies {
  implementation project(':hail')
}

mainClassName = 'is.hail.services.shuffler.ShuffleServer'
sourceCompatibility = '1.8'

ext {
  vertexVersion = '3.9.0'
}

dependencies {
  implementation "io.vertx:vertx-core:${vertexVersion}"
  implementation "io.vertx:vertx-web:${vertexVersion}"
  implementation 'com.indeed:lsmtree-core:1.0.7'
  implementation 'com.indeed:util-serialization:1.0.31'
  implementation 'com.indeed:util-mmap:1.0.31'
}

shadowJar {
  zip64 true
  classifier = 'fat'
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

task shadowTestJar(type: ShadowJar) {
    classifier = 'test'
    from sourceSets.test.output
    configurations = [project.configurations.testRuntime]
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.0'
}
